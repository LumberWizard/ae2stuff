buildscript {
    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            name = "gradle plugins"
            url = "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        //classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.0.9"
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
//apply plugin: 'com.matthewprenger.cursegradle'

file "build.properties" withReader {
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

if (project.hasProperty('forgeOverride')) {
    config.forge.version = forgeOverride
}

if (project.hasProperty('buildnum')) {
    ext.simpleVersion = "${config.ae2stuff.version}.${project.buildnum}"
} else {
    ext.simpleVersion = "${config.ae2stuff.version}-DEV"
}

version = simpleVersion + '-mc' + config.minecraft.version

group = "net.bdew"
archivesBaseName = "ae2stuff"

minecraft {
    version = "${config.minecraft.version}-${config.forge.version}"
    mappings = "${config.minecraft.mappings}"
    useDepAts = true
    makeObfSourceJar = false
    replace("AE2STUFF_VER", simpleVersion.toString())
}

repositories {
    mavenCentral()
    maven {
        name "JEI"
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        name "HWYLA"
        url "http://tehnut.info/maven"
    }
}

dependencies {
    deobfCompile "mezz.jei:jei_${config.minecraft.version}:${config.jei.version}"
    deobfCompile "mcp.mobius.waila:Hwyla:${config.hwyla.version}"

    deobfCompile "appeng:appliedenergistics2:${config.ae2.version}"

}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    inputs.property "tokens", minecraft.replacements
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        filter(ReplaceTokens, tokens: minecraft.replacements)
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task sourceJarReal(type: Jar) {
    classifier "sources"
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    exclude "**/*.psd"
    classifier "dev"
    duplicatesStrategy "exclude"
}


jar {
    exclude "**/*.psd"
}

artifacts {
    archives sourceJarReal
    archives deobfJar
}

apply plugin: 'maven-publish'

publishing {
    publications {
        maven(MavenPublication) {
            artifact deobfJar
            artifact sourceJarReal
        }
    }
    repositories {
        maven {
            url "file://var/www/maven"
        }
    }
}

// curseforge {
//     apiKey = project.hasProperty("curseForgeApiKey") ? project.curseForgeApiKey : ""
//     project {
//         id = config.curseforge.id

//         releaseType = "alpha"
//         changelog = project.hasProperty("changelog") ? project.changelog : "No changelog available"

//         addGameVersion config.minecraft.version
//         addGameVersion "1.12"
//         addGameVersion "1.12.2"


//         mainArtifact(jar) {
//             displayName = "AE2Stuff ${simpleVersion} (MC ${config.minecraft.version})"
//         }

//         addArtifact(deobfJar) {
//             displayName = "AE2Stuff ${simpleVersion} Deobfuscated (MC ${config.minecraft.version})"
//         }

//         addArtifact(sourceJarReal) {
//             displayName = "AE2Stuff ${simpleVersion} Source (MC ${config.minecraft.version})"
//         }

//         relations {
//             requiredLibrary 'applied-energistics-2'
//             optionalLibrary 'waila'
//             optionalLibrary 'just-enough-items-jei'
//         }
//     }
// }